//@version=3
//
// Average Daily Range (ADR) Indicator for TradingView
// This script is based on the "Best ADR Indicator for MT4" described at https://www.fxdayjob.com/best-adr-indicator-mt4.
// by Sherwin Daganato, 20171119
// https://github.com/sherwind/pinescript-adr
//
// Inputs:
//
// Number of ADR Back - Set the number of calendar days back to plot historical ADR. The default value is 7.
//                      Non-trading days are not taken into account. 
//                      A value of 7, for example, would display only 5 ADR for a 24x5 market.
// Number of AWR Back - Set the number of calendar weeks back to plot historical AWR. The default value is 0.
// Number of AMR Back - Set the number of calendar months back to plot historical AMR. The default value is 0.
// Number of AYR Back - Set the number of calendar years back to plot historical AYR. The default value is 0.
// ADR Length parameter - Set the length parameter of ADR. The default value is 1.
// WDR Length parameter - Set the length parameter of AWR. The default value is 1.
// MDR Length parameter - Set the length parameter of AMR. The default value is 1.
// YDR Length parameter - Set the length parameter of AYR. The default value is 1.
//
study(title="SD - Average Daily Range - Daily Weekly Monthly Yearly", shorttitle="[SD]ADR", overlay=true)

daily_adr = input(title="Number of ADR Back", type=integer, defval=7, minval=0)
weekly_adr = input(title="Number of AWR Back", type=integer, defval=0, minval=0)
monthly_adr = input(title="Number of AMR Back", type=integer, defval=0, minval=0)
yearly_adr = input(title="Number of AYR Back", type=integer, defval=0, minval=0)
daily_adr_length = input(title="ADR Length parameter", type=integer, defval=1, minval=0)
weekly_adr_length = input(title="AWR Length parameter", type=integer, defval=1, minval=0)
monthly_adr_length = input(title="AMR Length parameter", type=integer, defval=1, minval=0)
yearly_adr_length = input(title="AYR Length parameter", type=integer, defval=1, minval=0)

new_bar(res) => change(time(res)) != 0
new_period(condition, src) =>
    result = 0.0
    result := condition ? src : result[1]
    result
adr(length) =>
    range = high - low
    sma(range[1], length)
adr_high(adr_length) =>
    adr_ = adr(adr_length)
    (high - low) < adr_ ? low + adr_ : close >= open ? low + adr_ : high
adr_low(adr_length) =>
    adr_ = adr(adr_length)
    (high - low) < adr_ ? high - adr_ : close >= open ? low : high - adr_


//
// Daily
//
day_adr_high = security(tickerid, 'D', adr_high(daily_adr_length), lookahead=barmerge.lookahead_on)
day_adr_low = security(tickerid, 'D', adr_low(daily_adr_length), lookahead=barmerge.lookahead_on)

one_day = 1000 * 60 * 60 * 24
new_day = daily_adr > 0 and timenow - time < one_day * daily_adr and new_bar("D")
day_adr_high_ = new_period(new_day, day_adr_high)
day_adr_low_ = new_period(new_day, day_adr_low)

plot(isintraday or isdaily ? day_adr_high_ : na, title="ADR high", style=circles, color=lime, linewidth=2)
plot(isintraday or isdaily ? day_adr_low_ : na, title="ADR low", style=circles, color=lime, linewidth=2)


//
// Weekly
//
week_adr_high = security(tickerid, 'W', adr_high(weekly_adr_length), lookahead=barmerge.lookahead_on)
week_adr_low = security(tickerid, 'W', adr_low(weekly_adr_length), lookahead=barmerge.lookahead_on)

one_week = one_day * 7
new_week = weekly_adr > 0 and timenow - time < one_week * weekly_adr and new_bar("W")
week_adr_high_ = new_period(new_week, week_adr_high)
week_adr_low_ = new_period(new_week, week_adr_low)

plot(isintraday or isdaily or isweekly ? week_adr_high_ : na, title="AWR high", style=circles, color=orange, linewidth=2)
plot(isintraday or isdaily or isweekly ? week_adr_low_ : na, title="AWR low", style=circles, color=orange, linewidth=2)


//
// Monthly
//
month_adr_high = security(tickerid, 'M', adr_high(monthly_adr_length), lookahead=barmerge.lookahead_on)
month_adr_low = security(tickerid, 'M', adr_low(monthly_adr_length), lookahead=barmerge.lookahead_on)

one_month = one_day * 30
new_month = monthly_adr > 0 and timenow - time < one_month * monthly_adr and new_bar("M")
month_adr_high_ = new_period(new_month, month_adr_high)
month_adr_low_ = new_period(new_month, month_adr_low)

plot(isintraday or isdwm ? month_adr_high_ : na, title="AMR high", style=circles, color=fuchsia, linewidth=2)
plot(isintraday or isdwm ? month_adr_low_ : na, title="AMR low", style=circles, color=fuchsia, linewidth=2)


//
// Yearly
//
year_adr_high = security(tickerid, '12M', adr_high(yearly_adr_length), lookahead=barmerge.lookahead_on)
year_adr_low = security(tickerid, '12M', adr_low(yearly_adr_length), lookahead=barmerge.lookahead_on)

one_year = one_day * 365
new_year = yearly_adr > 0 and timenow - time < one_year * yearly_adr and new_bar("12M")
year_adr_high_ = new_period(new_year, year_adr_high)
year_adr_low_ = new_period(new_year, year_adr_low)

plot(year_adr_high_, title="AYR high", style=circles, color=red, linewidth=2)
plot(year_adr_low_, title="AYR low", style=circles, color=red, linewidth=2)
