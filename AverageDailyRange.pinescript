//@version=3
//
// Average Daily Range (ADR) Indicator for TradingView
// This script is based on the "Best ADR Indicator for MT4" described at https://www.fxdayjob.com/best-adr-indicator-mt4.
// by Sherwin Daganato, 20171119
// https://github.com/sherwind/pinescript-adr
//
// Inputs:
//
// Number of ADR Back - Set the number of calendar days back to plot historical ADR. The default value is 7.
//                      Non-trading days are not taken into account. 
//                      A value of 7, for example, would display only 5 ADR for a 24x5 market.
// Number of AWR Back - Set the number of calendar weeks back to plot historical AWR. The default value is 0.
// Number of AMR Back - Set the number of calendar months back to plot historical AMR. The default value is 0.
// Number of AYR Back - Set the number of calendar years back to plot historical AYR. The default value is 0.
// ADR Length parameter - Set the length parameter of ADR. The default value is 1.
// WDR Length parameter - Set the length parameter of AWR. The default value is 1.
// MDR Length parameter - Set the length parameter of AMR. The default value is 1.
// YDR Length parameter - Set the length parameter of AYR. The default value is 1.
//
study(title="SD - Average Daily Range - Daily Weekly Monthly Yearly", shorttitle="[SD]ADR", overlay=true)

daily_adr = input(title="Number of ADR Back", type=integer, defval=7, minval=0)
weekly_adr = input(title="Number of AWR Back", type=integer, defval=0, minval=0)
monthly_adr = input(title="Number of AMR Back", type=integer, defval=0, minval=0)
yearly_adr = input(title="Number of AYR Back", type=integer, defval=0, minval=0)
daily_adr_length = input(title="ADR Length parameter", type=integer, defval=1, minval=0)
weekly_adr_length = input(title="AWR Length parameter", type=integer, defval=1, minval=0)
monthly_adr_length = input(title="AMR Length parameter", type=integer, defval=1, minval=0)
yearly_adr_length = input(title="AYR Length parameter", type=integer, defval=1, minval=0)

new_bar(res) => change(time(res)) != 0
new_period(condition, src) =>
    result = 0.0
    result := condition ? src : result[1]
    result
adr_high(tf_range, tf_adr, tf_open, tf_high, tf_low, tf_close) =>
    tf_range < tf_adr ? tf_low + tf_adr : tf_close >= tf_open ? tf_low + tf_adr : tf_high
adr_low(tf_range, tf_adr, tf_open, tf_high, tf_low, tf_close) =>
    tf_range < tf_adr ? tf_high - tf_adr : tf_close >= tf_open ? tf_low : tf_high - tf_adr

//
// Daily
//
day_high = security(tickerid, 'D', high, lookahead=barmerge.lookahead_on)
day_low = security(tickerid, 'D', low, lookahead=barmerge.lookahead_on)
day_open = security(tickerid, 'D', open, lookahead=barmerge.lookahead_on)
day_close = security(tickerid, 'D', close, lookahead=barmerge.lookahead_on)
day_range = day_high - day_low
day_adr = sma(day_range[1], daily_adr_length)

day_adr_high = adr_high(day_range, day_adr, day_open, day_high, day_low, day_close)
day_adr_low = adr_low(day_range, day_adr, day_open, day_high, day_low, day_close)

one_day = 1000 * 60 * 60 * 24
new_day = daily_adr > 0 and timenow - time < one_day * daily_adr and new_bar("D")
day_adr_high_ = new_period(new_day, day_adr_high)
day_adr_low_ = new_period(new_day, day_adr_low)

plot(isintraday or isdaily ? day_adr_high_ : na, title="ADR high", style=circles, color=lime, linewidth=2)
plot(isintraday or isdaily ? day_adr_low_ : na, title="ADR low", style=circles, color=lime, linewidth=2)

//
// Weekly
//
week_high = security(tickerid, 'W', high, lookahead=barmerge.lookahead_on)
week_low = security(tickerid, 'W', low, lookahead=barmerge.lookahead_on)
week_open = security(tickerid, 'W', open, lookahead=barmerge.lookahead_on)
week_close = security(tickerid, 'W', close, lookahead=barmerge.lookahead_on)
week_range = week_high - week_low
week_adr = sma(week_range[1], weekly_adr_length)

week_adr_high = adr_high(week_range, week_adr, week_open, week_high, week_low, week_close)
week_adr_low = adr_low(week_range, week_adr, week_open, week_high, week_low, week_close)

one_week = one_day * 7
new_week = weekly_adr > 0 and timenow - time < one_week * weekly_adr and new_bar("W")
week_adr_high_ = new_period(new_week, week_adr_high)
week_adr_low_ = new_period(new_week, week_adr_low)

plot(isintraday or isdaily or isweekly ? week_adr_high_ : na, title="AWR high", style=circles, color=orange, linewidth=2)
plot(isintraday or isdaily or isweekly ? week_adr_low_ : na, title="AWR low", style=circles, color=orange, linewidth=2)

//
// Monthly
//
month_high = security(tickerid, 'M', high, lookahead=barmerge.lookahead_on)
month_low = security(tickerid, 'M', low, lookahead=barmerge.lookahead_on)
month_open = security(tickerid, 'M', open, lookahead=barmerge.lookahead_on)
month_close = security(tickerid, 'M', close, lookahead=barmerge.lookahead_on)
month_range = month_high - month_low
month_adr = sma(month_range[1], monthly_adr_length)

month_adr_high = adr_high(month_range, month_adr, month_open, month_high, month_low, month_close)
month_adr_low = adr_low(month_range, month_adr, month_open, month_high, month_low, month_close)

one_month = one_day * 30
new_month = monthly_adr > 0 and timenow - time < one_month * monthly_adr and new_bar("M")
month_adr_high_ = new_period(new_month, month_adr_high)
month_adr_low_ = new_period(new_month, month_adr_low)

plot(isintraday or isdwm ? month_adr_high_ : na, title="AMR high", style=circles, color=fuchsia, linewidth=2)
plot(isintraday or isdwm ? month_adr_low_ : na, title="AMR low", style=circles, color=fuchsia, linewidth=2)

//
// Yearly
//
year_high = security(tickerid, '12M', high, lookahead=barmerge.lookahead_on)
year_low = security(tickerid, '12M', low, lookahead=barmerge.lookahead_on)
year_open = security(tickerid, '12M', open, lookahead=barmerge.lookahead_on)
year_close = security(tickerid, '12M', close, lookahead=barmerge.lookahead_on)
year_range = year_high - year_low
year_adr = sma(year_range[1], yearly_adr_length)

year_adr_high = adr_high(year_range, year_adr, year_open, year_high, year_low, year_close)
year_adr_low = adr_low(year_range, year_adr, year_open, year_high, year_low, year_close)

one_year = one_day * 365
new_year = yearly_adr > 0 and timenow - time < one_year * yearly_adr and new_bar("12M")
year_adr_high_ = new_period(new_year, year_adr_high)
year_adr_low_ = new_period(new_year, year_adr_low)

plot(year_adr_high_, title="AYR high", style=circles, color=red, linewidth=2)
plot(year_adr_low_, title="AYR low", style=circles, color=red, linewidth=2)
